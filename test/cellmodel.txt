##
# Supplementary material
#
# From Weisse et al., "A mechanistic link between cellular trade-offs, 
# gene expression and growth", PNAS, 2015
#
# Using the formalism from Siso-Nadal F, Ollivier JF, Swain PS, Facile: 
# a command-line network compiler for systems biology, BMC Syst Biol. 2007;1:36. 
# Compiler code available from swainlab.bio.ed.ac.uk


EQN

# import

null => si ; k0 = et*vt*s0/(Kt + s0)

# metabolism

si => null ; nucat
null => a ; k = ns*nucat 
a => null ; ttrate 

# transcription

null => mr ;	k1 = wr*a/(thetar + a) 
null => mt ; 	k2 = we*a/(thetax + a) 
null => mm ; 	k2 
null => mp ; 	k4 = wp*a/(thetax + a) 
null => mq ; 	k5 = wq*a/(thetax + a)/(1 + (q/Kq)^nq)

# translation

r + mr <-> rmr ;	kb= 1; ku= 1.0
r + mt <-> rmt ;	kb ; ku
r + mm <-> rmm ;	kb ; ku
r + mp <-> rmp ;	kb ; ku
r + mq <-> rmq ;	kb ; ku
rmr -> r + r + mr ;	k6 = gamma/nr
rmt -> r + et + mt ; 	k7 = gamma/nx
rmm -> r + em + mm ;	k7
rmp -> r + p + mp ;	k7
rmq -> r + q + mq ; 	k7
 
# degradation
mr -> null ; dm= 0.1
mt -> null ; dm
mm -> null ; dm
mp -> null ; dm
mq -> null ; dm


rmr <-> zmr ; f= cl*k_cm; b=0
rmt <-> zmt ; f; b
rmm <-> zmm ; f; b
rmp <-> zmp ; f; b
rmq <-> zmq ; f; b

# dilution
mr -> null ; lam
mt -> null ; lam
mm -> null ; lam
mp -> null ; lam
mq -> null ; lam
rmr -> null ; lam
rmt -> null ; lam
rmm -> null ; lam
rmp -> null ; lam
rmq -> null ; lam
r -> null ; lam
et -> null ; lam
em -> null ; lam
p -> null ; lam
q -> null ; lam
a -> null ; lam
si -> null ; lam
zmr -> null ; lam
zmt -> null ; lam
zmm -> null ; lam
zmp -> null ; lam
zmq -> null ; lam


PARAMS
# parameters obtained from MCMC sampling
 Kp = 180.1378030928276
 thetar = 426.8693338968694
 k_cm = 0.005990373118888
 wr = 929.9678874564831
 wq = 948.9349882947897
 we = 4.139172187824451
 Kq = 1.522190403737490e5
 thetax = 4.379733394834643

# we found wp to be 0 from Copasi's optimization
wp = 0.0

# translation rate
gmax= 1260.0
Kgamma= gmax/Kp
gamma= gmax*a/(Kgamma + a)

# growth rate
M= 1.0e8
ttrate = (rmq + rmr + rmp + rmt + rmm)*gamma
lam = ttrate/M
fr = nr*(r + rmr + rmp + rmt + rmm + rmq + zmr + zmp + zmt + zmm + zmq) / ( nr*(r + rmr + rmp + rmt + rmm + rmq + zmr + zmp + zmt + zmm + zmq) + nx * (p + q + et + em))

nr= 7549.0
nx= 300.0

vm= 5800.0
Km= 1.0e3
ns= 0.5
nucat= em*vm*si/(Km + si)

vt= 726.0
Kt= 1.0e3
s0= 1.0e4

nq= 4

INIT
# initial conditions
r= 10.0
a= 1000.0



#nr*a + cr => r + mr + r; nur = cr * gamma / nr 
#nt*a + ct => r + mt + et; nut = ct * gamma / nx
#nq*a + cq => r + mq + q; nuq = cq * gamma /nx



#si => ns * a; vcat = em*vm*si/(Km+si)

UNSCALABLE


